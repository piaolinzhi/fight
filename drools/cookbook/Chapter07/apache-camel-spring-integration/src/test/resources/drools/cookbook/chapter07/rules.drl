package drools.cookbook.chapter07

import drools.cookbook.chapter07.model.Server
import drools.cookbook.chapter07.model.Virtualization

rule "check minimum server configuration"
dialect "mvel"
when
    $server : Server(processors < 2 || memory<=1024 || diskSpace<=250)
then
    System.out.println("Server \"" + $server.name + "\" was rejected by don't apply the minimum configuration.");
    retract($server);
end

rule "check available server for a new virtualization"
dialect "mvel"
when
    $virtualization : Virtualization($virtMemory : memory, $virtDiskSpace : diskSpace)
    $server : Server($memory : memory, $diskSpace : diskSpace, virtualizations !=null)
    Number((intValue + $virtMemory) < $memory) from accumulate 
        (Virtualization($vmemory : memory) from $server.virtualizations, sum($vmemory))
    Number((intValue + $virtDiskSpace) < $diskSpace) from accumulate
        (Virtualization($vdiskSpace : diskSpace) from $server.virtualizations, sum($vdiskSpace))
then
    $server.addVirtualization($virtualization);
    retract($virtualization);
end
